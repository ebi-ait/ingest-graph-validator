## Test: ensure_lane_index

#### Test description

If more than one R1/R2/I1/I2 in a set, then ensure lane indices are specified.

If more than one set of R1/R2/I1/I2 SEQUENCE_FILEs belong to a single  process (sequencing experiment) AND those files lack any lane_index, then this snippet returns an error.


#### The test
[source,cypher]
----
MATCH (p:process)-[:DERIVED_FILES]->(f:sequence_file)
WHERE f.read_index IN [ 'read1', 'read2', 'index1', 'index2']
WITH
      p
    , f.read_index AS read_index
    , collect(f.`file_core.file_name`) AS file_names
    , collect(f.lane_index) AS lane_indexes
    , count(*) AS fileCount
WHERE fileCount > 1 // Check that there are multiple sequence files for each type [ 'read1', 'read2', 'index1', 'index2'] belonging to a single process so that lane_index is required.
  AND size(lane_indexes) <> fileCount //The total number of sequence files and the number of sequence files that have a lane_index in each category must be equal to pass the test.
RETURN p, replace("If more than 3 files are linked to a sequencing process, lane indices must be noted. Files in this process: {}", "{}", toString(fileCount)), labels(p)
----
